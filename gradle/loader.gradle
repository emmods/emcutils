architectury.platformSetupLoomIde()

File commonAW = project ':common' file 'src/main/resources/emcutils.accesswidener'
loom.accessWidenerPath.set commonAW

String projectName = "${name.substring(0, 1).toUpperCase()}${name.substring(1)}"

if (projectName == 'Neoforge') projectName = 'NeoForge'

configurations {
	common {
		canBeResolved = true
		canBeConsumed = false
	}

	compileClasspath.extendsFrom common
	runtimeClasspath.extendsFrom common

	shadowBundle {
		canBeResolved = true
		canBeConsumed = false
	}
}

dependencies {
	common(project(path: ':common', configuration: 'namedElements')) { transitive = false }
	shadowBundle project(path: ':common', configuration: "transformProduction$projectName")
}

shadowJar {
	configurations = [project.configurations.shadowBundle]
	archiveClassifier = 'dev-shadow'
}

remapJar {
	inputFile.set shadowJar.archiveFile

	//archiveClassifier = null
}

//jar.archiveClassifier = 'dev'

sourcesJar {
	def commonSources = project(':common').sourcesJar
	dependsOn commonSources
	from commonSources.archiveFile.map { zipTree(it) }
	exclude 'emcutils.accesswidener'
}

//components.java {
//	withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
//		skip()
//	}
//}

modrinth {
	projectId = 'emcutils'
	versionName = "${System.getenv().VERSION_NAME ?: versionNumber} ($projectName)"
	changelog = System.getenv().CHANGELOG ?: DEFAULT_CHANGELOG
	versionType = System.getenv().VERSION_TYPE ?: DEFAULT_VERSION_TYPE
	uploadFile = remapJar
	gameVersions = [libs.versions.minecraft.get()]
	loaders = [name]
}
