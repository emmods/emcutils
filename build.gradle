plugins {
	alias libs.plugins.architectury.plugin
	alias libs.plugins.architectury.loom apply false
	alias libs.plugins.loom.quiltflower apply false
	//alias libs.plugins.machete
}

String mcVer = libs.versions.minecraft.get()
architectury.minecraft = mcVer

subprojects {
	apply plugin: 'dev.architectury.loom'
	apply plugin: 'io.github.juuxel.loom-quiltflower'

	repositories {
		[
			['Modrinth', 'api.modrinth.com/maven', ['maven.modrinth'], []],
			['CurseMaven', 'cfa2.cursemaven.com', ['curse.maven'], []],
			['QuiltMC', 'maven.quiltmc.org/repository/release', ['org.quiltmc'], ['org\\.quiltmc\\..*']],
		].forEach { var mavenInfo ->
			exclusiveContent {
				forRepository {
					maven {
						name = mavenInfo[0]
						url = 'https://' + mavenInfo[1]
					}
				}
				filter {
					mavenInfo[2].forEach { String group ->
						includeGroup group
					}
					mavenInfo[3].forEach { String group ->
						includeGroupByRegex group
					}
				}
			}
		}
	}

	dependencies {
		minecraft libs.minecraft
		mappings variantOf(libs.yarn) { classifier 'v2' }
	}

	quiltflower.addToRuntimeClasspath = true

	loom {
		runtimeOnlyLog4j = true
		clientOnlyMinecraftJar()

		runs {
			configureEach {
				vmArg '-Dmixin.debug.export=true'
			}
		}
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'
	//apply plugin: 'io.github.p03w.machete'

	group = 'coffee.waffle'
	archivesBaseName = "emcutils-$project.name-$mcVer"
	version = rootProject.mod_version

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
		options.release.set 17
	}

	java.withSourcesJar()

	jar.from 'LICENSE'
}
