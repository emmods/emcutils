plugins {
	alias libs.plugins.architectury.plugin
	alias libs.plugins.architectury.loom apply false
}

String mcVer = libs.versions.minecraft.get()
architectury.minecraft = mcVer

subprojects {
	apply plugin: 'dev.architectury.loom'

	repositories {
		[
			['Modrinth', 'api.modrinth.com/maven', ['maven.modrinth'], []],
			['NeoForge', 'maven.neoforged.net/releases', [], []],
		].forEach { var mavenInfo ->
			exclusiveContent {
				forRepository {
					maven {
						name = mavenInfo[0]
						url = 'https://' + mavenInfo[1]
					}
				}
				filter {
					mavenInfo[2].forEach { String group ->
						includeGroup group
					}
					mavenInfo[3].forEach { String group ->
						includeGroupByRegex group
					}
				}
			}
		}
	}

	dependencies {
		minecraft libs.minecraft
		mappings loom.layered() {
			it.mappings variantOf(libs.yarn) { classifier 'v2' }
			it.mappings libs.yarn.patch
		}
	}

	loom {
		runtimeOnlyLog4j = true

		runs {
			configureEach {
				vmArg '-Dmixin.debug.export=true'
			}
		}
	}
}

allprojects {
	apply plugin: 'java'
	apply plugin: 'architectury-plugin'
	apply plugin: 'maven-publish'

	group = 'coffee.waffle'
	archivesBaseName = "emcutils-$project.name-$mcVer"
	version = rootProject.mod_version

	tasks.withType(JavaCompile).configureEach {
		options.encoding = 'UTF-8'
		options.release.set 21
	}

	java.withSourcesJar()

	jar.from 'LICENSE'
}
