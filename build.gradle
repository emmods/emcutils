plugins {
  id "architectury-plugin" version "3.4.+"
  id "dev.architectury.loom" version "0.10.0.+" apply false
  id "com.modrinth.minotaur" version "1.2.+"
  id "io.github.juuxel.loom-quiltflower-mini" version "1.+" apply false
}

architectury {
  minecraft = rootProject.mc_version
}

subprojects {
  apply plugin: "dev.architectury.loom"
  apply from: rootProject.file("setupAuth.gradle")
  apply plugin: "io.github.juuxel.loom-quiltflower-mini"

  repositories {
    maven {
      url "https://api.modrinth.com/maven"
      content { includeGroup "maven.modrinth" }
    }
  }

  dependencies {
    minecraft "com.mojang:minecraft:${rootProject.mc_version}"
    mappings "net.fabricmc:yarn:${rootProject.mc_version}+build.${rootProject.yarn_mappings}:v2"

    if (project.name != "forge") {
      modImplementation "net.fabricmc:fabric-loader:${rootProject.loader_version}"

      modApi "maven.modrinth:midnightlib:${rootProject.midnightlib_version}"
    }

    implementation annotationProcessor("org.projectlombok:lombok:${rootProject.lombok_version}")
  }
}

allprojects {
  apply plugin: "java"
  apply plugin: "architectury-plugin"
  apply plugin: "maven-publish"

  group = "coffee.waffle"
  archivesBaseName = "emcutils-${project.name}-${rootProject.mc_version}"
  version = mod_version + System.getenv().SNAPSHOT

  // Comment this line out if you have good internet
  idea { module { downloadJavadoc = false } }

  tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    //noinspection all
    options.release = 16
  }

  java { withSourcesJar() }

  jar { from "LICENSE" }

  //noinspection UnnecessaryQualifiedReference
  task modrinth(type: com.modrinth.minotaur.TaskModrinthUpload) {
    final String loader = project.name
    onlyIf { System.getenv().MODRINTH_TOKEN }
    detectLoaders = false
    token = System.getenv().MODRINTH_TOKEN
    projectId = "QYTT62S0"
    versionNumber = mod_version + "+" + loader
    versionName = "${rootProject.release_title} (${loader.substring(0, 1).toUpperCase() + loader.substring(1)})"
    changelog = rootProject.changeLog
    uploadFile = project.file("build/libs/${rootProject.archivesBaseName}-${rootProject.version}.jar")
    addGameVersion(mc_version)
    addLoader(loader)
  }

  publishing {
    repositories {
      maven {
        url = System.getenv().MAVEN_URL
        credentials {
          username = System.getenv().MAVEN_USER
          password = System.getenv().MAVEN_PASS
        }
        authentication {
          //noinspection GroovyAssignabilityCheck
          basic(BasicAuthentication)
        }
      }
    }
  }
}