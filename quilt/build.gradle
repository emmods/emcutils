plugins {
	alias libs.plugins.minotaur
	alias libs.plugins.shadow
	alias libs.plugins.loom.quiltflower
}

apply from: '../gradle/loader.gradle'
apply from: '../gradle/quilt-based.gradle'

architectury.loader 'quilt'

File commonAW = project ':common' file 'src/main/resources/emcutils.accesswidener'
File commonMixins = project ':common' file 'src/main/resources/emcutils-common.mixins.json'
File generatedResources = file 'src/generated/resources'

configurations {
	developmentQuilt.extendsFrom common
}

dependencies {
	modImplementation libs.midnightlib
	include libs.midnightlib

	modRuntimeOnly libs.modmenu
	modRuntimeOnly libs.lazydfu

	include project(':journey')
	include project(':xaero')
}

sourceSets.main.resources.srcDir generatedResources

task copyFromCommon(type: Copy) {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE

	from commonAW
	from commonMixins
	into generatedResources
}

processResources.dependsOn copyFromCommon

sourcesJar {
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

modrinth {
	dependencies {
		required.project 'qsl'
		embedded.project 'midnightlib'
		optional.project 'modmenu'
		optional.project 'journeymap'
	}
	syncBodyFrom = rootProject.file('README.md').text
}
tasks.modrinth.dependsOn tasks.modrinthSyncBody
